def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def versionProperties = new Properties()
def versionPropertiesFile = rootProject.file('../version.properties')
if (versionPropertiesFile.exists()) {
    versionPropertiesFile.withReader('UTF-8') { reader ->
        versionProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = versionProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = versionProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.jeppeman.locallydynamic'

android {
    compileSdkVersion 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "in.canews.zeronetmobile"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release
            minifyEnabled = false
            shrinkResources = false
        }

        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.release
            locallyDynamic {
                enabled = false
                throttleDownloadBy = 1000
            }
            defaultConfig.ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86_64'
        }
    }
    dynamicFeatures = [":arm64", ":arm", ":common", ":x86", ":x86_64", 
    ":arm64_python", ":arm_python", ":common_python", ":x86_python", ":x86_64_python",
    ":arm64_tor", ":arm_tor", ":x86_tor", ":x86_64_tor"
    ]


}

flutter {
    source '../..'
}

dependencies {
//    def billing_version = '4.0.0'
//    implementation "com.android.billingclient:billing:$billing_version"

    def work_version = "2.7.0"
    // Force WorkManager 2.6.0 for transitive dependency
    implementation("androidx.work:work-runtime-ktx:$work_version") {
        force = true
    }
    
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.gms:play-services-base:17.6.0'
    
    //Duplicate Classes Issue https://stackoverflow.com/a/60492942
    // implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    debugImplementation 'com.jeppeman.locallydynamic:locallydynamic-debug:0.3'
    releaseImplementation 'com.jeppeman.locallydynamic:locallydynamic:0.3'

    // testImplementation 'junit:junit:4.12'
    // androidTestImplementation 'androidx.test:runner:1.2.0'
    // androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
